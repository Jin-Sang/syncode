start: predicate_section premise_section conclusion_section

predicate_section: "Predicates:" predicate_definition+
premise_section: "Premises:" premise+
conclusion_section: "Conclusion:" conclusion+

predicate_definition: PREDICATE "(" VAR ("," VAR)* ")" COMMENT  -> define_predicate
premise: quantified_expr COMMENT -> define_premise
conclusion: expression COMMENT -> define_conclusion

quantified_expr: quantifier VAR "(" expression ")" | expression
quantifier: "∀" -> forall | "∃" -> exists

expression: implication_expr

?implication_expr: xor_expr ("→" implication_expr)?  -> imply
?xor_expr: or_expr ("⊕" xor_expr)?                -> xor
?or_expr: and_expr ("∨" or_expr)?                -> or
?and_expr: neg_expr ("∧" and_expr)?              -> and
?neg_expr: "¬" atom -> neg | atom

?atom: PREDICATE "(" VAR ("," VAR)* ")" -> predicate 
    | "(" expression ")" 

// Variable names begin with a lowercase letter
VAR.-1: /[a-z][a-zA-Z0-9_]*/  | /[0-9]+/

// Predicate names begin with a capital letter
PREDICATE.-1: /[A-Z][a-zA-Z]*/

COMMENT: /:::.*\n/

%import common.WS
%ignore WS
